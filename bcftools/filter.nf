process FILTER {
    tag "${cohort}:${key}:${category}"

    label 'simple'
	label 'bcftools'

    publishDir("${params.output_dir}/filtered/", mode: 'copy')

    input:
    tuple val(cohort), val(key),
          path(file), path(index),
          val(n_variants),
          val(category)

    output:
    tuple val(cohort), val(key), val(category),
          path("${cohort}.${key}.${category}.vcf.gz"),
          path("${cohort}.${key}.${category}.vcf.gz.tbi"),
          env(n_variants)
        
    script:
    """
    #!/bin/bash
    # Filter variants
    bcftools view -i "${params.AC_COL} >= ${params.AC}" ${file} | \
    if   [ "${params.remove_benign}" = "true" ];      then bcftools +split-vep -a ${params.vep_tag} -s worst -c CLIN_SIG -e "CLIN_SIG ~ 'benign'"; else bcftools view; fi | \
    if   [ '${params.remove_vus}' = 'true' ]; then bcftools +split-vep -a ${params.vep_tag} -s worst -c CLIN_SIG -e "CLIN_SIG ~ 'conflicting'"; else bcftools view ; fi | \
    if   [ '${params.remove_lc}' = 'true' ];  then bcftools +split-vep -a ${params.vep_tag} -s worst -c LoF -e "LoF = 'LC'"; else bcftools view ; fi | \
    if   [ "${params.freq_tag}" = "VEP"  ];   then bcftools +split-vep -a ${params.vep_tag} -s worst -c ${params.AF_COL}:Float -e "${params.AF_COL} > ${params.AF}"; else bcftools filter -e "${params.AF_COL} > ${params.AF}" ; fi | \
    if   [ "${category}" = "Rare" ];       then bcftools view;
    elif [ "${category}" = "Pathogenic" ]; then bcftools +split-vep -a ${params.vep_tag} -s worst -c CLIN_SIG -i "CLIN_SIG ~ 'pathogenic' || CLIN_SIG ~ 'likely_pathogenic'";
    elif [ "${category}" = "High" ];       then bcftools +split-vep -a ${params.vep_tag} -s worst -c IMPACT,CADD_PHRED:Float -i "IMPACT='HIGH' && CADD_PHRED > ${params.CADD}";
    elif [ "${category}" = "Damaging" ];   then bcftools +split-vep -a ${params.vep_tag} -s worst -c IMPACT,CADD_PHRED:Float -i "(IMPACT='HIGH' || IMPACT='MODERATE') && CADD_PHRED > ${params.CADD}";
    elif [ "${category}" = "PTV" ];        then bcftools +split-vep -a ${params.vep_tag} -s worst -c Consequence -i "Consequence~'stop_gained' || Consequence~'frameshift_variant' || Consequence~'splice_acceptor_variant'";
    elif [ "${category}" = "Stop" ];       then bcftools +split-vep -a ${params.vep_tag} -s worst -c Consequence -i "Consequence~'stop_gained'";
    elif [ "${category}" = "Splicing" ];   then bcftools +split-vep -a ${params.vep_tag} -s worst -c SpliceAI_pred_DS_AG:Float,SpliceAI_pred_DS_AL:Float,SpliceAI_pred_DS_DG:Float,SpliceAI_pred_DS_DL:Float -i "SpliceAI_pred_DS_AG > ${params.DS} || SpliceAI_pred_DS_AL > ${params.DS} || SpliceAI_pred_DS_DG > ${params.DS} || SpliceAI_pred_DS_DL > ${params.DS}";
    elif [ "${category}" = "Unfiltered" ]; then bcftools view;
    else exit "Category: ${category} is not recognized"; fi | \
    bcftools annotate --set-id '%CHROM:%POS:%REF:%ALT' | \
    bcftools view --threads ${task.cpus} -Oz -o ${cohort}.${key}.${category}.vcf.gz

    # Index the VCF
    tabix ${cohort}.${key}.${category}.vcf.gz
    
    # Count the number of samples and variants
    n_variants=\$(bcftools index -n ${cohort}.${key}.${category}.vcf.gz)
    """
}
